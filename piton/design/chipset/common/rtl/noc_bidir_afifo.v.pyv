// ========== Copyright Header Begin ============================================
// Copyright (c) 2015 Princeton University
// All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//     * Redistributions of source code must retain the above copyright
//       notice, this list of conditions and the following disclaimer.
//     * Redistributions in binary form must reproduce the above copyright
//       notice, this list of conditions and the following disclaimer in the
//       documentation and/or other materials provided with the distribution.
//     * Neither the name of Princeton University nor the
//       names of its contributors may be used to endorse or promote products
//       derived from this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY PRINCETON UNIVERSITY "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL PRINCETON UNIVERSITY BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ========== Copyright Header End ============================================

//==================================================================================================
//  Filename      : mig_async_fifo.v
//  Created On    : 2015-08-25
//  Revision      :
//  Author        : Yaosheng Fu
//  Company       : Princeton University
//  Email         : yfu@princeton.edu
//
//  Description   : Async FIFO between MIG user interface and FPGA for converting clock
//  TODO          : change wires names *_splitter to more common
//
//==================================================================================================

<% 
from pyhplib import *
%>
`include "define.tmp.h"

// *_in_*_1 --> *out_*_2
// *_out_*_1 <-- *_in_*_2
module noc_bidir_afifo #(
    parameter ENABLE_TRAFFIC_SHAPER = 0    
) (
    input wire          clk_1,
    input wire          rst_1,
    
    input wire          clk_2,
    input wire          rst_2,
    
    // Request direction
    input wire                         flit_in_val_1,
    input wire [`NOC_DATA_WIDTH-1:0]   flit_in_data_1,
    output wire                        flit_in_rdy_1,

    output wire                        flit_out_val_2,
    output wire [`NOC_DATA_WIDTH-1:0]  flit_out_data_2,
    input wire                         flit_out_rdy_2,
    
    // Response direction
    input wire                         flit_in_val_2,
    input wire [`NOC_DATA_WIDTH-1:0]   flit_in_data_2,
    output wire                        flit_in_rdy_2,

    output wire                        flit_out_val_1,
    output wire [`NOC_DATA_WIDTH-1:0]  flit_out_data_1,
    input wire                         flit_out_rdy_1
);

wire fifo_send_full;
wire fifo_send_empty;
wire fifo_send_rd_en;
wire fifo_send_wr_en;
reg  fifo_send_empty_reg; 


afifo_w64_d128_std async_fifo_send(
    .rst(rst_2),
    .wr_clk(clk_1),
    .rd_clk(clk_2),
    .rd_en(fifo_send_rd_en),
    .wr_en(fifo_send_wr_en),
    .din(flit_in_data_1),
    .dout(flit_out_data_2),     // data: 1 cycle delay after rd_en
    .full(fifo_send_full),
    .empty(fifo_send_empty),
    .wr_rst_busy(send_wr_rst_busy),  // output wire wr_rst_busy
    .rd_rst_busy(send_rd_rst_busy)  // output wire rd_rst_busy
);


always @ (posedge clk_2) begin
    if (rst_2)
        fifo_send_empty_reg <= 1'b1;
    else
        fifo_send_empty_reg <= fifo_send_rd_en ? 1'b0 
                             : flit_out_rdy_2  ? 1'b1 
                             :                   fifo_send_empty_reg;
end

assign fifo_send_wr_en = flit_in_val_1 & ~send_wr_rst_busy;
assign fifo_send_rd_en = ~fifo_send_empty & (fifo_send_empty_reg | flit_out_rdy_2) & ~send_rd_rst_busy;
assign flit_out_val_2 = ~fifo_send_empty_reg;
assign flit_in_rdy_1 = ~fifo_send_full & ~send_wr_rst_busy;


generate
if (ENABLE_TRAFFIC_SHAPER == 0) begin
    
wire fifo_recv_full;
wire fifo_recv_empty;
wire fifo_recv_rd_en;
wire fifo_recv_wr_en;
reg  fifo_recv_empty_reg;

afifo_w64_d128_std async_fifo_recv(
    .rst(rst_1),
    .wr_clk(clk_2),
    .rd_clk(clk_1),
    .rd_en(fifo_recv_rd_en),
    .wr_en(fifo_recv_wr_en),
    .din(flit_in_data_2),
    .dout(flit_out_data_1),   // data: 1 cycle delay after rd_en
    .full(fifo_recv_full),
    .empty(fifo_recv_empty), 
    .wr_rst_busy(recv_wr_rst_busy),  // output wire wr_rst_busy
    .rd_rst_busy(recv_rd_rst_busy)  // output wire rd_rst_busy
);

always @(posedge clk_1) begin
    if (rst_1)
        fifo_recv_empty_reg <= 1'b1;
    else
        fifo_recv_empty_reg <= fifo_recv_rd_en ? 1'b0 
                             : flit_out_rdy_1  ? 1'b1 
                             :                   fifo_recv_empty_reg;
end

assign fifo_recv_wr_en = flit_in_val_2 & ~recv_wr_rst_busy;
assign fifo_recv_rd_en = ~fifo_recv_empty & (fifo_recv_empty_reg | flit_out_rdy_1) & ~recv_rd_rst_busy;
assign flit_out_val_1 = ~fifo_recv_empty_reg;
assign flit_in_rdy_2 = ~fifo_recv_full & ~recv_wr_rst_busy;

end
else begin

traffic_shaper #(
    .DATA_WIDTH(`NOC_DATA_WIDTH), 
<%
print(f"    .ADD_LATENCY_SLOW({PITON_MC_LAT}),")
print(f"    .ADD_LATENCY_FAST({PITON_MC_LAT}),")
%>
    .PKGS_PER_128_CYCLES_FAST(0), 
    .PKGS_PER_128_CYCLES_SLOW(0)
) noc_mc_shaper (
    .clk_out(clk_1),
    .rst_n_out(~rst_1),
    .clk_in(clk_2), 
    .rst_n_in(~rst_2),
    .fast(1'b0),
    .valid_in(flit_in_val_2),
    .data_in(flit_in_data_2),
    .ready_in(flit_in_rdy_2),
    .valid_out(flit_out_val_1),
    .data_out(flit_out_data_1),
    .ready_out(flit_out_rdy_1)
);

end
endgenerate

endmodule
